#
# A single line theme with Git information on the left and Ruby on the right.
#
# Authors:
#   Kyle West <kswest@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Features:
#   - Indicates dirty Git repository.
#   - Indicates the Ruby version.
#   - Indicates vi modes.
#
# Screenshots:
#   http://i.imgur.com/dCwhynn.png
#

function prompt_alt_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Ruby information.
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi

  _prompt_pwd=$(promptpwd)
}

function prompt_alt_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_alt_precmd

  local _zsh_colors
  typeset -A _zsh_colors
  if [[ `tput colors` = 256 ]]; then
    #_zsh_colors[bg]='%K{17}'
    _zsh_colors[bg]=''
    _zsh_colors[dir]="%F{39}"
    _zsh_colors[err]='%F{196}'
    _zsh_colors[usr]='%F{28}'
    _zsh_colors[host]='%B%F{49}'
    _zsh_colors[time]='%F{136}'
  else
    _zsh_colors[bg]=''
    _zsh_colors[dir]='%F{cyan}'
    _zsh_colors[err]='%F{red}'
    _zsh_colors[usr]='%F{blue}'
    _zsh_colors[host]='%B%F{cyan}'
    _zsh_colors[time]='%F{magenta}'
  fi

  _BG=$_zsh_colors[bg]

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format "%B%F{green}❯%f%b"
  zstyle ':prezto:module:editor:info:keymap:alternate' format "%B%F{magenta}❮%f%b"

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'no'
  zstyle ':prezto:module:git:info:branch' format '%F{yellow}%b%f'
  zstyle ':prezto:module:git:info:dirty' format '%B%F{red}!%f%b'
  zstyle ':prezto:module:git:info:indexed' format ' '
  zstyle ':prezto:module:git:info:unindexed' format ' '
  zstyle ':prezto:module:git:info:untracked' format ' '
  zstyle ':prezto:module:git:info:keys' format 'prompt' '- %b%D '

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '%F{blue}[%v]%f'

  # Define prompts.
  #PROMPT='%F{cyan}%c%f ${git_info:+${(e)git_info[prompt]}}${editor_info[keymap]} '
  #PROMPT='%(?..%F{red}[%?] )%F{cyan}${_prompt_pwd} ${git_info:+${(e)git_info[prompt]}}${editor_info[keymap]} '
  #PROMPT='%(?..%F{red}[%?] )${_zsh_colors[dir]}${_prompt_pwd} ${git_info:+${(e)git_info[prompt]}}${editor_info[keymap]} '
  PROMPT=$_BG'%(?..'$_zsh_colors[err]'[%?] )'$_zsh_colors[dir]'${_prompt_pwd} ${git_info:+${(e)git_info[prompt]}}${editor_info[keymap]}'$_BG' '$_BG
  #RPROMPT='${ruby_info[version]}'
  #RPROMPT='%F{blue}%n%f@%B%F{cyan}%m%f %F{magenta}%t'
  RPROMPT=$_zsh_colors[usr]'%n%f@'$_zsh_colors[host]'%m%f '$_zsh_colors[time]'%t'
}

prompt_alt_setup "$@"
